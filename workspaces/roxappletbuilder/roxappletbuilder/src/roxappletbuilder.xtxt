Applet:
	"applet" name = ID "{"
		"group" group = STRING
	"}"
	("texts" "{"
		(texts += Text)*
	"}")?
	("images" "{"
		(images += Image)*
	"}")?
	(models += Model)*
	(composites += Composite)*
	"gui" "{"
		"rootComposite" rootComposite = [Composite|ID]
	"}";
	
Text:
	"text" name = ID "=" defaultValue = STRING ";";

Model:
	"model" name = ID "{"
		(attributes += ModelAttribute)*
	"}";

ModelAttribute:
	"attribute" name = ID "=" defaultValue = STRING ";";

Composite:
	"composite" name = ID "{"
		("menus" "{"
			(menus += Menu)*
		"}")?
		rootWidget = Widget
	"}";

Image:
	"image" name = ID "{"
		source = ImageSource
		("resize" width = INT "," height = INT ";")? 
	"}";
	
ImageSource:
	ImageSourceFile;
	
ImageSourceFile:
	"file" name = STRING ";";

Widget:
	AbstractMenuItem | Button | HBox | Label | Menu | VBox;
	
Button:
	"button" (name = ID)? "{"
		("title" title = [Text|ID] ";")?
		("image" image = [Image|ID] ";")?
		(handlers += ButtonHandler)*
	"}";
	
ButtonHandler:
	ClickedHandler | ModelChangeHandler | PopupHandler;

HBox:
	"hbox" (name = ID)? "{"
		(homogeneous ?= "homogeneous")?
		("spacing" spacing = INT ";")?
		(widgets += Widget)*
	"}";
	
Label:
	"label" (name = ID)? "{"
		(wrap ?= "wrap")?
		("angle" angle = INT ";")?
		("label" label = [Text|ID] ";")?
		(handlers += LabelHandler)*
	"}";
	
LabelHandler:
	ClickedHandler | ModelChangeHandler | PopupHandler;

Menu:
	"menu" name = ID "{"
		(menuItems += AbstractMenuItem)*
	"}";
	
VBox:
	"vbox" (name = ID)? "{"
		(homogeneous ?= "homogeneous")?
		("spacing" spacing = INT ";")?
		(widgets += Widget)*
	"}";
	
AbstractMenuItem:
	ImageMenuItem | MenuItem;

ImageMenuItem:
	"image-menu-item" (name = ID)? "{"
	"}";
	
MenuItem:
	"menu-item" (name = ID)? "{"
		("title" title = [Text|ID] ";")?
	"}";

Handler:
	ClickedHandler | ModelChangeHandler | PopupHandler;

ClickedHandler:
	"clicked-handler" "{" "}";

ModelChangeHandler:
	"model-change-handler" "{"
		("attribute" attributes += [ModelAttribute|ID] ";")*
	"}";

PopupHandler:
	"popup-handler" "{"
		"menu" menu = [Menu|ID] ";"
	"}";
